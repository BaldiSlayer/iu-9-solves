## Задание 1
```scheme
(define (exit) (display "you have to write (use-assertions) before 'assert' using"))

(define-syntax use-assertions
  (syntax-rules ()
    ((use-assertions)
     (call-with-current-continuation
       (lambda (cont)
         (set! exit cont))))))

(define-syntax assert
  (syntax-rules ()
    ((assert expr)
     (if (not expr)
         (begin
           (display "FAILED: ")
           (write 'expr)
           (newline)
           (exit))))))

(use-assertions) ; Инициализация вашего каркаса перед использованием

; Определение процедуры, требующей верификации переданного ей значения:

(define (1/x x)
  (assert (not (zero? x))) ; Утверждение: x ДОЛЖЕН БЫТЬ ≠ 0
  (/ 1 x))

; Применение процедуры с утверждением:

(map 1/x '(-2 -1 0 1 2)) ; ВЫВЕДЕТ в консоль сообщение и завершит работу программы
```

## Задание 2

### Сериализация
``` scheme
(define (save-data data path)
        (with-output-to-file path (lambda ()
                                    (write data))))

(define (load-data path)
        (with-input-from-file path (lambda ()
                                    (read))))
```

### Подсчет количества строчек
```scheme
(define (file-nonempty-lines-count path)
          (with-input-from-file path (lambda ()
                                       (let loop ((prev #\newline)
                                                  (k 0))
                                         (let ((cur (read-char)))
                                           (if (eof-object? cur)
                                               (if (equal? #\newline prev)
                                                   k
                                                   (+ k 1))
                                               (if (and (equal? #\newline cur)
                                                        (not (equal? #\newline prev)))
                                                   (loop cur (+ k 1))
                                                   (loop cur k))))))))

(display (file-nonempty-lines-count "./name.smth"))
```

## Задание 3
```scheme
(define tribonacci-memo
  (let ((dp '()))
    (lambda (n)
      (let* ((args n)
             (res (assoc args dp)))
        (if res
            (cadr res)
            (let ((res (cond ((= n 0) 0)
                             ((<= n 2) 1)
                             (else (+ (tribonacci-memo (- n 3))
                                      (tribonacci-memo (- n 2))
                                      (tribonacci-memo (- n 1)))))))
              (set! dp (cons (list args res) dp))
              res))))))

(tribonacci-memo 9000)
 ```
 
 Очевидно, что решение с мемоизацией будет выигрывать по времени, поскольку нам не надо будет пересчитывать заново значения, которые мы уже однажды считали
 Доказательство.
 Если мы будем считать числа Трибоначчи, то для подсчета n-ого числа нам также нужны число трибоначчи с номером (n-1), (n-2), (n-3). Тогда в худшем случае ассимптотика этого алгорита будет O(3^N). Тогда как ассимптотика алгоритма с мемоизацией - O(N)

## Задание 4

```scheme
(define-syntax my-if
  (syntax-rules ()
    ((my-if condition statement1 statement2)
     (let ((promise1 (delay statement1))
           (promise2 (delay statement2)))
       (force (or (and condition promise1) promise2))))))
```

## Задание 5

```scheme
(define-syntax my-let
  (syntax-rules ()
    ((my-let ()
             expr)
     expr)
    ((my-let ((var1 expr1) (varn exprn) ...)
             expr)
     ((lambda (var1)
        (my-let ((varn exprn) ...)
                expr))
      expr1))))
          
(define-syntax my-let*
  (syntax-rules ()
    ((my-let* ()
              expr)
     expr)
    ((my-let* ((var1 expr1) (varn exprn) ...)
              expr)
     ((lambda (var1)
        (my-let* ((varn exprn) ...)
                 expr))
      expr1))))
```

## Задание 6
А.


```scheme
(define-syntax when
  (syntax-rules ()
    ((when test expr ...)
     (if test
         (begin expr ...)))))

(define-syntax unless
  (syntax-rules ()
    ((unless test expr ...)
     (if (not test)
         (begin expr ...)))))
```

Б.

```scheme
(define-syntax for
  (syntax-rules (in as)
    ((for x in xs expr ...)
     (let loop ((values xs))
       (if (not (null? values))
           (let ((x (car values)))
             (begin expr ...)
             (loop (cdr values))))))
    ((for xs as x expr ...)
     (for x in xs expr ...))))
```
    
В.


```scheme
(define-syntax while
  (syntax-rules ()
    ((while cond? expr ...)
     (let loop ()
       (if cond?
           (begin expr ...
                  (loop)))))))
```
    
Г.


```scheme
(define-syntax repeat
  (syntax-rules (until)
    ((repeat (expr ...) until cond?)
     (let loop ()
       (begin expr ...
              (if (not cond?) (loop)))))))
```
    
Если убрать скобки, то от скобок можно избавиться, убрав их и из макроса


```scheme
(define-syntax repeat
  (syntax-rules (until)
    ((repeat expr ... until cond?)
     (let loop ()
       (begin expr ...
              (if (not cond?) (loop)))))))
```

Д.


```scheme
(define-syntax cout
  (syntax-rules (<< endl)
    ((cout << endl)
     (newline))
    ((cout << elem)
     (display elem))
    ((cout << endl others ...)
     (begin (newline)
            (cout others ...)))
    ((cout << elem others ...)
     (begin (display elem)
            (cout others ...)))))  
```
