% Лабораторная работа № 6. Оболочка и скрипты
% 12 января 2023 г.
% Алексей Лисов, ИУ9-11Б

# Цели работы
Получение навыков реализации лексических анализаторов и 
нисходящих синтаксических анализаторов,
использующих метод рекурсивного спуска.

## Вспомогательная структура данных — поток (stream)

```scheme
;; Конструктор потока
(define (make-stream items . eos)
  (if (null? eos)
      (make-stream items #f)
      (list items (car eos))))

;; Запрос текущего символа
(define (peek stream)
  (if (null? (car stream))
      (cadr stream)
      (caar stream)))

;; Запрос первых двух символов
(define (peek2 stream)
  (if (null? (car stream))
      (cadr stream)
      (if (null? (cdar stream))
          (list (caar stream))
          (list (caar stream) (cadar stream)))))

;; Продвижение вперёд
(define (next stream)
  (let ((n (peek stream)))
    (if (not (null? (car stream)))
        (set-car! stream (cdr (car stream))))
    n))

(define (expect stream term error)
    (if (equal? (peek stream) term)
        (next stream)
        (error #f)))    
```

# Задание 1

### `check-frac`

#### БНФ
```
<frac>            ::= <signed-num> / <unsigned-num>
<signed-num>      ::= + <unsigned-num> | - <unsigned-num> | <unsigned-num>
<unsigned-num>    ::= <num-tail>
<num-tail>        ::= <num-tail> | <empty>
<empty>           ::=
```

### Реализация
```scheme
(define (check-frac str)
  (load "stream.scm") 

  (define (signed-num stream error)
    (cond ((equal? #\+ (peek stream))
           (next stream)
           (unsigned-num stream error))
          ((equal? #\- (peek stream))
           (next stream)
           (unsigned-num stream error))
          (else (unsigned-num stream error))))
  
  (define (unsigned-num stream error)
    (cond ((and (char? (peek stream))
                (char-numeric? (peek stream)))
           
           (next stream)
           (num-tail stream error))
          (else (error #f))))
          
  (define (num-tail stream error)
    (cond ((and (char? (peek stream))
                (char-numeric? (peek stream)))
           (next stream)
           (num-tail stream error))
          (else #t)))
  
  (define (frac stream error)
    (signed-num stream error)
    (expect stream #\/ error)
    (unsigned-num stream error))   
  
  
  
  ;; создаем поток
  (define stream (make-stream (string->list str) 'EOF))
  
  ;; Создаём точку возврата
  (call-with-current-continuation
   (lambda (error)
     (frac stream error)
     (eqv? (peek stream) 'EOF))))
```

### Тестирование
```
> (check-frac "1/2")
#t
> (check-frac "inf/2")
#f
> (check-frac "1/ui")
#f
> (check-frac "oi/dd")
#f
> (check-frac "12")
#f
> (check-frac "")
#f
```

### `scan-frac`

#### БНФ

```
<frac>            ::= <signed-num> / <unsigned-num>
<signed-num>      ::= + <unsigned-num> | - <unsigned-num> | <unsigned-num>
<unsigned-num>    ::= <num-tail>
<num-tail>        ::= <num-tail> | <empty>
<empty>           ::=
```

### Реализация

```scheme
(define (scan-frac str)
  (load "stream.scm")

  (define (signed-num stream error)
    (cond ((equal? #\+ (peek stream))
           (next stream)
           (unsigned-num stream error))
          ((equal? #\- (peek stream))
           (next stream)
           (- (unsigned-num stream error)))
          (else (unsigned-num stream error))))
  
  (define (unsigned-num stream error)
    (cond ((and (char? (peek stream))
                (char-numeric? (peek stream)))
           (string->number (list->string
                            (cons (next stream)
                                  (num-tail stream error)))))
          (else (error #f))))
  
  (define (num-tail stream error)
    (cond ((and (char? (peek stream))
                (char-numeric? (peek stream)))
           (cons (next stream)
                 (num-tail stream error)))
          (else '())))
  
  (define (frac stream error)
    (define numerator (signed-num stream error))
    (expect stream #\/ error)
    (/ numerator
       (unsigned-num stream error))) 
  
  ;; Создаем поток
  (define stream (make-stream (string->list str) 'EOF))

  ;; Создаём точку возврата
  (call-with-current-continuation
   (lambda (error)
     (define res (frac stream error))
     (and (eqv? (peek stream) 'EOF)
          res))))
```

### Тестирование
```
> (scan-frac "-1/2")
-1/2
> (scan-frac "inf/2")
#f
> (scan-frac "1/ui")
#f
> (scan-frac "oi/dd")
#f
> (scan-frac "12")
#f
> (scan-frac "")
#f
```

### `scan-many-fracs`

#### БНФ

```
<list-fracs>      ::= <spaces> <frac> <spaces> <сlist-fracs> | <empty>
<spaces>          ::= <spaces> | <empty>
<frac>            ::= <signed-num> / <unsigned-num>
<signed-num>      ::= + <unsigned-num> | - <unsigned-num> | <unsigned-num>
<unsigned-num>    ::= <num-tail>
<num-tail>        ::= <num-tail> | <empty>
<empty>           ::=

```

### Реализация

```scheme
(define (scan-many-fracs str)
  (define (is_all_spaces str)
    (if (null? str)
        '()
        (if (not (char-whitespace? (car str)))
            #f
            (is_all_spaces (cdr str)))))
            
  (load "stream.scm")  
  
  (define (frac-list stream error)
    (cond ((and (char? (peek stream))
                (or (char-whitespace? (peek stream))
                    (equal? (peek stream) #\+)
                    (equal? (peek stream) #\-)
                    (char-numeric? (peek stream))))
           (spaces stream error)
           (let ((new-frac (frac stream error)))
             (spaces stream error)
             (cons new-frac (frac-list stream error))))
          (else '())))
  
  (define (spaces stream error)
    (cond ((and (char? (peek stream))
                (char-whitespace? (peek stream)))
           (next stream)
           (spaces stream error))
          (else #t)))

  (define (signed-num stream error)
    (cond ((equal? #\+ (peek stream))
           (next stream)
           (unsigned-num stream error))
          ((equal? #\- (peek stream))
           (next stream)
           (- (unsigned-num stream error)))
          (else (unsigned-num stream error))))
  
  (define (unsigned-num stream error)
    (cond ((and (char? (peek stream))
                (char-numeric? (peek stream)))
           (string->number (list->string
                            (cons (next stream)
                                  (num-tail stream error)))))
          (else (error #f))))
  
  (define (num-tail stream error)
    (cond ((and (char? (peek stream))
                (char-numeric? (peek stream)))
           (cons (next stream)
                 (num-tail stream error)))
          (else '())))
  
  (define (frac stream error)
      
    (define numerator (signed-num stream error))
    (expect stream #\/ error)
    (/ numerator
       (unsigned-num stream error))) 
  
  ;; создаем поток
  (define stream (make-stream (string->list str) 'EOF))

  ;; Создаём точку возврата
  (or (is_all_spaces (string->list str))
      (call-with-current-continuation
       (lambda (error)        
         (define res (frac-list stream error))
         (and (eqv? (peek stream) 'EOF)
              res)))))
```

### Тестирование
```
> (scan-many-fracs "\t1/2 1/3\n\n2/-5")
#f
> (scan-many-fracs "\t1/2 1/3\n\n10/8")
(1/2 1/3 1 1/4)
> (scan-many-fracs " ")
()
```

# Задание 2

### Реализация

```scheme
(define (parse tokens)
  (load "stream.scm")
  
  (define (program stream error)
    (let* ((t-articles (articles stream error))
           (t-body (body stream error)))
      (list t-articles t-body)))
  
  (define (articles stream error)
    (cond ((eqv? 'define (peek stream))
           (let* ((t-article (article stream error))
                  (t-articles (articles stream error)))
             (cons t-article t-articles)))
          (else '())))
  
  (define (article stream error)
    (let* ((t-define (expect stream 'define error))
           (t-word (next stream))
           (t-body (body stream error))
           (t-end (expect stream 'end error)))
      (list t-word t-body)))
  
  (define (body stream error)
    (cond ((eqv? 'if (peek stream))
           (let* ((t-if (next stream))
                  (t-body (body stream error))
                  (t-endif (expect stream 'endif error))
                  (t-body-tail (body stream error)))
             (cons (list 'if t-body) t-body-tail)))
          ((integer? (peek stream))
           (let* ((t-integer (next stream))
                  (t-body-tail (body stream error)))
             (cons t-integer t-body-tail)))
          ((and (symbol? (peek stream))
                (not (eqv? (peek stream) 'endif))
                (not (eqv? (peek stream) 'end)))
           (let* ((t-word (next stream))
                  (t-body-tail (body stream error)))
             (cons t-word t-body-tail)))
          (else '())))
  
  (define stream (make-stream (vector->list tokens) "EOF"))

  
  (call-with-current-continuation
   (lambda (error)
     (define res (program stream error))
     (and (equal? (peek stream) "EOF")
          res))))
```


### Тестирование
```
> (display (parse #(1 2 +)))
(() (1 2 +))
> (parse #(x dup 0 swap if drop -1 endif))
(() (x dup 0 swap (if (drop -1))))
> (parse #( define -- 1 - end
          define =0? dup 0 = end
          define =1? dup 1 = end
          define factorial
              =0? if drop 1 exit endif
              =1? if drop 1 exit endif
              dup --
              factorial
              *
          end
          0 factorial
          1 factorial
          2 factorial
          3 factorial
          4 factorial ))
(((-- (1 -)) (=0? (dup 0 =)) (=1? (dup 1 =)) (factorial (=0? (if (drop 1 exit)) =1? (if (drop 1 exit)) dup -- factorial *)))
 (0 factorial 1 factorial 2 factorial 3 factorial 4 factorial))
```

# Вывод
Я приобрел навыки реализации лексических анализаторов и 
нисходящих синтаксических анализаторов,
использующих метод рекурсивного спуска.
