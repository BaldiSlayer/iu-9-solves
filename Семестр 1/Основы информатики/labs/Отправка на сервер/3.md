% Лабораторная работа № 3. Типы данных. Модульное тестирование
% 13 января 2023 г.
% Алексей Лисов, ИУ9-11Б

# Цели работы
* На практике ознакомиться с системой типов языка Scheme.
* На практике ознакомиться с юнит-тестированием.
* Разработать свои средства отладки программ на языке Scheme.
* На практике ознакомиться со средствами метапрограммирования языка Scheme.

# Задание 1

## Реализация

Код `trace.scm`:
```scheme
(define-syntax trace-ex
  (syntax-rules ()
    ((trace-ex expr)
     (begin
       (write 'expr)
       (let ((res expr))
         (begin
           (display " => ")
           (write res)
           (newline)))))))     
```

Код `1.rkt`:
```scheme
(load "trace.scm")

(define (zip . xss)
  (if (or (null? xss)
          (null? (trace-ex (car xss))))
      '()
      (cons (map car xss)
            (apply zip (map cdr (trace-ex xss))))))
```

## Тестирование

```
> (zip '(1 2 3) '(one two three))
(car xss) => (1 2 3)
xss => ((1 2 3) (one two three))
(car xss) => (2 3)
xss => ((2 3) (two three))
(car xss) => (3)
xss => ((3) (three))
(car xss) => ()
((1 one) (2 two) (3 three))
```

# Задание 2

## Реализация

```scheme

(define-syntax test
  (syntax-rules ()
    ((test expr res)
     (list expr res 'expr))))

(define (run-test test)
  (let ((expr (car test))
        (res_exp (cadr test))
        (expr_literal (caddr test)))
    (begin
      (write expr_literal)
      (let* ((res_got expr)
             (is_passed (equal? res_exp res_got)))
        (begin (if is_passed
                   (display " ok\n")
                   (begin (display " FAIL\n")
                          (display "  Expected: ")
                          (write res_exp)
                          (display "\n  Returned: ")
                          (write res_got)
                          (newline)))
               is_passed)))))

(define (run-tests tests)
  (let loop ((res #t)
             (cnter 1)
             (tests tests))
    
    (if (null? tests)
        res
        (begin (display "Test ")
               (display cnter)
               (display ": ")
               (loop (and (run-test (car tests)) res)
                     (+ 1 cnter)
                     (cdr tests))))))

(define (signum x)
  (cond
    ((< x 0) -1)
    ((= x 0)  1) ; Ошибка здесь!
    (else     1)))

(define the-tests
  (list (test (signum -2) -1)
        (test (signum  0)  0)
        (test (signum  2)  1)))

(run-tests the-tests)
```

## Тестирование

```
Test 1: (signum -2) ok
Test 2: (signum 0) FAIL
  Expected: 0
  Returned: 1
Test 3: (signum 2) ok
#f
```

# Задание 3

## Реализация

```scheme
(define (list-insert! list index value)
  `(,@ (reverse (list-tail (reverse list) (- (length list) index))) ,(car value) ,@(list-tail list index)))

(define (ref object index . value)
  (and (>= index  0)
       (if (null? value)
           (cond ((list? object)
                  (and (< index (length object)) (list-ref object index)))
                 ((string? object)
                  (and (< index (string-length object)) (string-ref object index)))
                 ((vector? object)
                  (and (< index (vector-length object)) (vector-ref object index)))
                 (else #f))
           (cond ((list? object)
                  (and (<= index (length object)) (list-insert! object index value)))
                 ((string? object)
                  (and (<= index (string-length object))
                       (char? (car value))
                       (string-append (substring object 0 index) (string (car value)) (substring object index))))
                 ((vector? object)
                  (and (<= index (vector-length object)) (list->vector (list-insert! (vector->list object) index value))))
                 (else #f)))))
```

## Тестирование

```
Welcome to DrRacket, version 8.6 [cs].
Language: R5RS; memory limit: 128 MB.
> (ref '(1 2 3) 1)
2
> (ref #(1 2 3) 1)
2
> (ref "123" 1)
#\2
> (ref "123" 3)
#f
> (ref '(1 2 3) 1 0)
(1 0 2 3)
> (ref "123" 3 #\4)
"1234"
> (ref "123" 8 #\4)
#f
```


# Задание 4

## Реализация

```scheme
(define (factorize polynom)
  (let ((is_plus (equal? '+ (car polynom)))
        (is_power2 (= 2 (car (reverse (cadr polynom)))))
        (a (cadadr polynom))
        (b (car (cdaddr polynom))))
    (cond ((and (not is_plus) is_power2) ;; разность квадратов
           `(* (- ,a ,b) (+ ,a ,b)))
          ((and is_plus (not is_power2)) ;; сумма кубов
           `(* (+ ,a ,b) (+ (expt ,a 2) (- (* ,a ,b)) (expt ,b 2))))
          ((and (not is_plus) (not is_power2)) ;; разность кубов
           `(* (- ,a ,b) (+ (expt ,a 2) (* ,a ,b) (expt ,b 2)))))))

```

## Тестирование

```
> (factorize '(+ (expt a 3) (expt b 3)))
(* (+ a b) (+ (expt a 2) (- (* a b)) (expt b 2)))
> (eval (list (list 'lambda '(x y) (factorize '(- (expt x 2) (expt y 2)))) 1 2) (interaction-environment))
-3
```

# Вывод
Я на практике ознакомился с ситемой типов и со 
средствами метапрограммирования в языке Scheme и юнит-тестированием.
Разработал свои средства отладки программ на языке Scheme, 
для дальнейшего их применения
в следующих лабораторных работах и домашних заданиях.

